apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: 'constants.gradle'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    namespace "$mavenGroupId"
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "$mavenVersion"
        consumerProguardFiles 'proguard-rules.txt'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all {
        it.generateBuildConfigProvider.configure { enabled = false }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget=11
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "$mavenGroupId"
                artifactId = "$mavenArtifactId"
                version = "$mavenVersion"

                //artifact androidSourcesJar

                pom {
                    name = "$projectName"
                    description = "$projectDesc"
                    url = "https://quickverse.io"
                    licenses {
                        license {
                            name = 'Commercial'
                            url = 'https://github.com/QuickVerse/quickverse-android-sdk/blob/main/License.md'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = 'QuickVerse'
                        }
                    }
                    scm {
                        connection = "$githubURL"
                        developerConnection = "$githubURL"
                        url = 'https://github.com/QuickVerse/quickverse-android-sdk'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = mavenCentralUsername
                    password = mavenCentralPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}